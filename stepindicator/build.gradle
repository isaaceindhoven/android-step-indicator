plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

ext {
    mGroupId = "nl.isaac"
    mArtifactId = "android-step-indicator"
    mVersionCode = 2
    mVersionName = "1.0.1"

    mLibraryName = "android-step-indicator"
    mLibraryDescription = "Dynamic step indicator library for Android. Fully customisable and compatible with ViewPager2."
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31
        versionCode mVersionCode
        versionName mVersionName
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation("androidx.viewpager2:viewpager2:1.0.0")
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId mGroupId
                artifactId mArtifactId
                version mVersionName

                from components.release

                artifact androidSourcesJar

                pom {
                    name = mLibraryName
                    description = mLibraryDescription
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/isaaceindhoven/android-step-indicator")
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}

// Assembling should be performed before publishing package
publish.dependsOn assemble
